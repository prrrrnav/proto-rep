root.activity_id = match this.body.action {
    "create" => 1,
    "read" => 2,
    "update" => 3,
    "delete" => 4,
    "search" => 5,
    "import" => 6,
    "export" => 7,
    "share" => 8,
    "login" => 1,
    "password_reset" => 2,
    "logout" => 3,
    _ => 99
}


root.category_uid = 6
root.class_uid = if this.body.action == "login" || this.body.action == "password_reset" || this.body.action == "logout" {
    6002
} else {
    6001
}

root.app = if root.class_uid == 6002 {
    {
    "name": this.body.app_name.or("unknown_app"),
    "uid": this.body.app_uid.or("unknown_uid"),
    "vendor_name": this.body.vendor_name.or("unknown_vendor")
    }
} else {
    deleted()
}


root.metadata = {
    "product": {
        "name": this.body.app_name.or("unknown_app"),
        "vendor_name": this.body.vendor_name.or("unknown_vendor")
    },
    "version": "1.3.0"  # OCSF schema version
}

root.severity_id = match this.body.severity {
    "Unknown"       => 0,
    "Informational" => 1,
    "Low"           => 2,
    "Medium"        => 3,
    "High"          => 4,
    "Critical"      => 5,
    "Fatal"         => 6,
    "Other"         => 99
}


root.web_resources = if root.class_uid == 6001 {
    this.body.webResources.map_each({
    "name": this.resourceName,
    "type": this.resourceType
    })
} else {
    deleted()
}

root.time = this.body.timestamp.parse_timestamp("2006-01-02T15:04:05Z").ts_unix()

root.type_uid = root.class_uid * 100 + root.activity_id
